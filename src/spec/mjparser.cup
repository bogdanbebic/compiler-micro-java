
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;
import rs.ac.bg.etf.pp1.test.CompilerError;

parser code {:
    Logger log = Logger.getLogger(getClass());

    public void syntax_error(Symbol currentToken) {
        report_error("\nSyntax error", currentToken);
    }

    public void unrecovered_syntax_error(Symbol currentToken) throws Exception {
        report_fatal_error("Fatal error, parsing can't continue", currentToken);
    }

    public void report_fatal_error(String message, Object info) throws Exception {
        done_parsing();
        report_error(message, info);
    }

    public void report_error(String message, Object info) {
        StringBuilder msg = new StringBuilder(message);
        if (info instanceof Symbol)
            msg.append(" on line ").append(((Symbol)info).left);

        log.error(msg.toString());
        MJCompiler.getInstance().addError(new CompilerError(
            ((Symbol) info).left,
            "Invalid syntax",
            CompilerError.CompilerErrorType.SYNTAX_ERROR
        ));
    }
:}


scan with {:
    Symbol s = this.getScanner().next_token();
    if (s != null && s.value != null)
        log.info(s.toString() + " " + s.value.toString());
    return s;
:}

/* keywords */
terminal PROGRAM, BREAK, CLASS, ELSE, CONST, IF, SWITCH, DO, WHILE, NEW, PRINT,
    READ, RETURN, VOID, EXTENDS, CONTINUE, CASE, YIELD, DEFAULT;

/* literals */
terminal Integer NUMBER;
terminal Character CHAR;
terminal Integer BOOL;

/* identifiers */
terminal String IDENT;

/* operators */
terminal INC, DEC, PLUS, MINUS, MULT, DIV, MOD;
terminal EQ, NE, GE, LE, GT, LT;
terminal LAND, LOR;
terminal ASSIGN, SEMI, COMMA, DOT;
terminal LPAREN, RPAREN, LBRACKET, RBRACKET, LBRACE, RBRACE, COLON;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Program;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgramHeader;
nonterminal DeclList, MethodDeclList;
nonterminal ConstantDecl, VariableDecl, ClassDecl;
nonterminal ConstantDeclList, SingleConstantDecl, Constant;
nonterminal VariableDeclList, SingleVariableDecl, OptionalArraySpecifier;
nonterminal ClassDeclStart, ClassDeclEnd;
nonterminal OptionalInheritance, OptionalMethodDeclList;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type;
nonterminal MethodDecl;
nonterminal MethodSignature;
nonterminal MethodSignatureWithoutParams;
nonterminal MethodParams;
nonterminal ReturnType, FormalParams, VarDeclList, StatementList;
nonterminal FormalParamsList, SingleFormalParamDecl;
nonterminal Statement;
nonterminal DesignatorStatement;
nonterminal DoWhileStatement, DoWhileBodyStart, DoWhileBodyEnd;
nonterminal IfCondition, OptionalElseBranch;
nonterminal OptionalWidthSpecifier;
nonterminal Condition, ConditionTerm, ConditionFactor;
nonterminal OptionalExpr, Expr, ExprTermList, SwitchExpr;
nonterminal ExpressionTerm, Term, Factor;
nonterminal SwitchBodyStart, SwitchBodyEnd;
nonterminal CaseLabelList, CaseLabel;
nonterminal Designator;
nonterminal OptionalFunctionCall, ActualParams, ActualParamsList;
nonterminal OptionalArrayIndexing, ArrayIndexing, FieldAccess;
nonterminal AssignOp, RelOp, AddOp, MulOp;

precedence left ELSE;

Program ::= (Program) ProgramHeader:programHeader DeclList LBRACE MethodDeclList RBRACE
          ;

ProgramHeader ::= (ProgramHeader) PROGRAM IDENT:programName
                ;

DeclList ::= (NoDeclarations)      /* epsilon */
           | (ConstantDeclaration) DeclList ConstantDecl
           | (VariableDeclaration) DeclList VariableDecl
           | (ClassDeclaration)    DeclList ClassDecl
           ;

ConstantDecl ::= (ConstantDecl) CONST Type ConstantDeclList SEMI
               ;

ConstantDeclList ::= (FirstConstantDecl) SingleConstantDecl
                   | (ConstantListDecl)  ConstantDeclList COMMA SingleConstantDecl
                   ;

SingleConstantDecl ::= (SingleConstantDecl) IDENT:constantName ASSIGN Constant:constant
                     ;

Constant ::= (NumberConstant) NUMBER:value
           | (CharConstant)   CHAR:value
           | (BoolConstant)   BOOL:value
           ;

VariableDecl ::= (GlobalVariableDecl)    Type VariableDeclList SEMI
               | (ErroneousVariableDecl) error COMMA VariableDeclList SEMI
               | (ErroneousGlobalDecl)   error SEMI
               ;

VariableDeclList ::= (FirstVariableDecl) SingleVariableDecl
                   | (VariableListDecl)  VariableDeclList COMMA SingleVariableDecl
                   ;

SingleVariableDecl ::= (SingleVariableDecl) IDENT:variableName OptionalArraySpecifier
                     ;

OptionalArraySpecifier ::= (NoArraySpecifier) /* epsilon */
                         | (ArraySpecifier)   LBRACKET RBRACKET
                         ;

ClassDecl ::= (ClassDefinition) ClassDeclStart VarDeclList OptionalMethodDeclList ClassDeclEnd
            | (ErroneousFields) ClassDeclStart VarDeclList error LBRACE MethodDeclList RBRACE ClassDeclEnd
            ;

ClassDeclStart ::= (ClassDeclarationStart) CLASS IDENT:className OptionalInheritance LBRACE
                 | (ErroneousInheritance)  CLASS IDENT:className error LBRACE
                 ;

ClassDeclEnd ::= (ClassDeclEnd) RBRACE
               ;

OptionalInheritance ::= (NoInheritanceDecl) /* epsilon */
                      | (InheritanceDecl)   EXTENDS Type
                      ;

OptionalMethodDeclList ::= (NoClassMethodDeclList) /* epsilon */
                         | (ClassMethodDeclList)   LBRACE MethodDeclList RBRACE
                         ;

Type ::= (Type) IDENT:typename
       ;

MethodDeclList ::= (NoMethodDeclarations) /* epsilon */
                 | (MethodDeclarations)   MethodDeclList MethodDecl
                 ;

MethodDecl ::= (MethodDecl) MethodSignature VarDeclList LBRACE StatementList RBRACE
             ;

MethodSignature ::= (MethodSignature) MethodSignatureWithoutParams MethodParams
                  ;

MethodSignatureWithoutParams ::= (MethodSignatureWithoutParams) ReturnType:returnType IDENT:methodName
                               ;

MethodParams ::= (ValidMethodParams)     LPAREN FormalParams RPAREN
               | (ErroneousMethodParams) LPAREN error RPAREN
               ;

ReturnType ::= (NonVoidReturnType) Type:type
             | (VoidReturnType)    VOID
             ;

FormalParams ::= (NoFormalParams)   /* epsilon */
               | (FormalParamsDecl) FormalParamsList
               ;

FormalParamsList ::= (FirstFormalParamDecl) SingleFormalParamDecl
                   | (FormalParamsListDecl) FormalParamsList COMMA SingleFormalParamDecl
                   | (ErroneousFormalParam) error COMMA SingleFormalParamDecl
                   ;

SingleFormalParamDecl ::= (FormalParamDecl) Type SingleVariableDecl
                        ;

VarDeclList ::= (NoVarDeclarations) /* epsilon */
              | (VarDeclaration)    VarDeclList VariableDecl
              ;

StatementList ::= (NoStatements) /* epsilon */
                | (Stmt)         StatementList Statement
                ;

Statement ::= (DesignatorStmt) DesignatorStatement SEMI
            | (IfElseStmt)     IF IfCondition Statement OptionalElseBranch
            | (DoWhileStmt)    DoWhileStatement SEMI
            | (YieldStmt)      YIELD Expr SEMI
            | (BreakStmt)      BREAK SEMI
            | (ContinueStmt)   CONTINUE SEMI
            | (ReturnStmt)     RETURN OptionalExpr SEMI
            | (ReadStmt)       READ LPAREN Designator RPAREN SEMI
            | (PrintStmt)      PRINT LPAREN Expr OptionalWidthSpecifier RPAREN SEMI
            | (BlockStmt)      LBRACE StatementList RBRACE
            | (ErroneousStmt)  error SEMI
            ;

IfCondition ::= (ValidIfCondition)     LPAREN Condition RPAREN
              | (ErroneousIfCondition) LPAREN error RPAREN
              ;

OptionalElseBranch ::= (NoElseBranch) /* epsilon */
                     | (ElseBranch)   ELSE Statement
                     ;

OptionalWidthSpecifier ::= (NoWidthSpecifier) /* epsilon */
                         | (WidthSpecifier)   COMMA NUMBER
                         ;

DesignatorStatement ::= (AssignmentStmt)    Designator AssignOp Expr
                      | (FunctionCallStmt)  Designator LPAREN ActualParams RPAREN
                      | (DesignatorIncStmt) Designator INC
                      | (DesignatorDecStmt) Designator DEC
                      ;

DoWhileStatement ::= (DoWhileStatement) DoWhileBodyStart Statement DoWhileBodyEnd IfCondition
                   ;

DoWhileBodyStart ::= (DoWhileBodyStart) DO
               ;

DoWhileBodyEnd ::= (DoWhileBodyEnd) WHILE
                 ;

Condition ::= (FirstConditionTerm) ConditionTerm
            | (ConditionTermList)  Condition LOR ConditionTerm
            ;

ConditionTerm ::= (FirstConditionFactor) ConditionFactor
                | (ConditionFactorList)  ConditionTerm LAND ConditionFactor
                ;

ConditionFactor ::= (FirstConditionExpr) Expr
                  | (ConditionExprRelOp) Expr RelOp Expr
                  ;

OptionalExpr ::= (NoExpression) /* epsilon */
               | (Expression)   Expr
               ;

Expr ::= (ExprTermListDecl) ExprTermList
       | (SwitchExprDecl)   SwitchExpr
       ;

ExprTermList ::= (SingleTerm)      ExpressionTerm
               | (AddOpExpression) ExprTermList AddOp Term
               ;

SwitchExpr ::= (SwitchExpression) SWITCH LPAREN Expr RPAREN SwitchBodyStart CaseLabelList SwitchBodyEnd
             ;

SwitchBodyStart ::= (SwitchBodyStart) LBRACE
                  ;

SwitchBodyEnd ::= (SwitchBodyEnd) RBRACE
                ;

CaseLabelList ::= (NoCaseLabels)  /* epsilon */
                | (CaseLabelDecl) CaseLabelList CaseLabel
                ;

CaseLabel ::= (NonDefaultCaseLabel) CASE NUMBER COLON StatementList
            | (DefaultCaseLabel)    DEFAULT COLON StatementList
            ;

ExpressionTerm ::= (PositiveTerm) Term
                 | (NegativeTerm) MINUS Term
                 ;

Term ::= (SingleFactor) Factor
       | (MulOpTerm)    Term MulOp Factor
       ;

Factor ::= (DesignatorFactor)  Designator OptionalFunctionCall
         | (ConstantFactor)    Constant
         | (AllocationFactor)  NEW Type OptionalArrayIndexing
         | (ParenthesesFactor) LPAREN Expr RPAREN
         ;

Designator ::= (SingleIdentifier)       IDENT:designator
             | (DesignatorMemberAccess) Designator FieldAccess
             | (DesignatorArrayIndex)   Designator ArrayIndexing
             ;

OptionalFunctionCall ::= (NoFunctionCall) /* epsilon */
                       | (FunctionCall)   LPAREN ActualParams RPAREN
                       ;

ActualParams ::= (NoActualParams)   /* epsilon */
               | (ActualParamsDecl) ActualParamsList
               ;

ActualParamsList ::= (FirstActualParamDecl) Expr
                   | (ActualParamsListDecl) ActualParamsList COMMA Expr
                   ;

OptionalArrayIndexing ::= (NoArrayIndexing) /* epsilon */
                        | (ArrayIndex)      ArrayIndexing
                        ;

ArrayIndexing ::= (ArrayIndexing) LBRACKET Expr RBRACKET
                ;

FieldAccess ::= (FieldAccess) DOT IDENT:fieldName
              ;

AssignOp ::= (AssignOp) ASSIGN
           ;

RelOp ::= (EqualOp)        EQ
        | (NotEqualOp)     NE
        | (GreaterOp)      GT
        | (GreaterEqualOp) GE
        | (LessOp)         LT
        | (LessEqualOp)    LE
        ;

AddOp ::= (PlusOp)  PLUS
        | (MinusOp) MINUS
        ;

MulOp ::= (MultiplyOp) MULT
        | (DivideOp)   DIV
        | (ModuloOp)   MOD
        ;
